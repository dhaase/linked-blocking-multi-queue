apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

group = 'com.github.marianobarrios'
version = '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            pom.project {
               name 'org.github.marianobarrios:lbmq'
               description 'LinkedBlockingMultiQueue is an optionally-bounded blocking "multi-queue" based on linked nodes. '
               url 'http://github.com/marianobarrios/lbmq'
               scm {
                   url 'scm:git@github.com:marianobarrios/lbmq.git'
                   connection 'scm:git@github.com:marianobarrios/lbmq.git'
                   developerConnection 'scm:git@github.com:marianobarrios/lbmq.git'
               }
               licenses {
                   license {
                       name 'BSD-style'
                       url 'http://www.opensource.org/licenses/bsd-license.php'
                   }
               }
               developers {
                   developer {
                       name 'Mariano Barrios'
                       email 'marbar@gmail.com'
                   }
               }
           }
        }
    }
}